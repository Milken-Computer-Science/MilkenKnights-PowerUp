plugins {
    id "java"
    id "idea"
    id "edu.wpi.first.GradleRIO" version "2019.0.0-alpha-3"
    id 'org.hidetake.ssh' version '2.9.0'
}

def ROBOT_CLASS = "frc.team1836.robot.Robot"

// Define my targets (RoboRIO) and artifacts (deployable files)
// This is added by GradleRIO's backing project EmbeddedTools.
deploy {
    targets {
        target("roborio", edu.wpi.first.gradlerio.frc.RoboRIO) {
            // Team number is loaded either from the .wpilib/wpilib_preferences.json
            // or from command line. If not found an exception will be thrown.
            // You can use getTeamOrDefault(team) instead of getTeamNumber if you
            // want to store a team number in this file.
            team = getTeamNumber()
        }
    }
    artifacts {
        artifact('frcJava', edu.wpi.first.gradlerio.frc.FRCJavaArtifact) {
            targets << "roborio"
            // Debug can be overridden by command line, for use with VSCode
            debug = getDebugOrDefault(false)
        }
    }
}

// Defining my dependencies. In this case, WPILib (+ friends), CTRE Phoenix (Talon SRX)
// and NavX.
dependencies {
    compile wpilib()
    compile ctre()
    compile navx()
    compile pathfinder()
}

// Setting up my Jar File. In this case, adding all libraries into the main jar ('fat jar')
// in order to make them all available at runtime. Also adding the manifest so WPILib
// knows where to look for our Robot Class.
jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest edu.wpi.first.gradlerio.GradleRIOPlugin.javaManifest(ROBOT_CLASS)
}

// Force Java 8 Compatibility mode for deployed code, in case the develoment
// system is using Java 10.
compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

wrapper {
    gradleVersion = '4.9'
}

remotes {
    webServer {
        host = '10.18.36.2'
        user = 'admin'
    }
}

ssh.settings {
    knownHosts = allowAnyHosts
}

task swerdUpdate {
    doLast {
        FileTree myFileTree = fileTree(dir: 'paths')
        def date = new Date()
        String formattedDate = date.format("yyyy-MM-dd").toString() + '/'
        def logBase = new File('logs')
        logBase.mkdirs()
        def logDir = new File(logBase.absolutePath + '/' + formattedDate)
        logDir.mkdirs()
        ssh.run {
            session(remotes.webServer) {
                put from: myFileTree.getFiles(), into: '/home/lvuser/paths'
                get from: '/u', into: 'logs/' + formattedDate, filter:{ it.name =~ /\.csv$/ }
                get from: '/u', into: 'logs/', filter:{ it.name =~ /\.txt$/ }
                remove '/u'
            }
        }
    }
}

task deployAll {
    dependsOn 'swerdUpdate'
    dependsOn 'deploy'
    tasks.findByName('deploy').mustRunAfter 'swerdUpdate'
}

